import com.google.common.collect.ImmutableList
import com.google.common.collect.ImmutableMap
import org.objectweb.asm.ClassReader
import org.objectweb.asm.ClassWriter
import org.objectweb.asm.tree.ClassNode
import org.objectweb.asm.tree.LdcInsnNode

import java.net.http.HttpClient
import java.net.http.HttpRequest
import java.net.http.HttpResponse

apply(plugin: "java")
apply(plugin: "fabric-loom")
apply(plugin: "maven-publish")
apply(plugin: "com.replaymod.preprocess")

int mcVersion = 1

preprocess {
    mcVersion = vars.get().get("MC")
}


repositories {
    maven {
        name = "Masa"
        url = "https://masa.dy.fi/maven"
        content {
            includeGroupAndSubgroups("fi.dy.masa")
        }
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
        content {
            includeGroupAndSubgroups("curse.maven")
        }
    }
    maven {
        name = "FabricMC"
        url = "https://maven.fabricmc.net"
        content {
            includeGroupAndSubgroups("net.fabricmc")
        }
    }
    maven {
        // 快捷潜影盒1.20.6以下所需要的
        name = "Kyrptonaught Maven"
        url = "https://maven.kyrptonaught.dev"
        content {
            includeGroupAndSubgroups("net.kyrptonaught")
        }
    }
    maven {
        // ModMenu
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases"
        content {
            includeGroupAndSubgroups("com.terraformersmc")
        }
    }
    maven {
        name "Nucleoid"
        url "https://maven.nucleoid.xyz/"
        content {
            includeGroupAndSubgroups("eu.pb4")
        }
    }
    maven {
        name = "Shedaniel Maven"
        url = "https://maven.shedaniel.me/"
        content {
            includeGroupAndSubgroups("me.shedaniel.cloth")
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroupAndSubgroups("maven.modrinth")
        }
    }
    maven {
        name = "CottonMC"
        url = "https://server.bbkr.space/artifactory/libs-release"
        content {
            includeGroupAndSubgroups("io.github.cottonmc")
        }
    }
    maven {
        name = "jackfredReleases"
        url = "https://maven.jackf.red/releases"
        content {
            includeGroupAndSubgroups("red.jackf")
        }
    }
    maven {
        name = "BlameJared"
        url = uri("https://maven.blamejared.com")
        content {
            includeGroupAndSubgroups("com.blamejared.searchables")
        }
    }
    maven {
        // 拼音搜索
        name = "Pinyin4j"
        url = "https://mvnrepository.com/artifact/com.belerweb/pinyin4j"
        content {
            includeGroupAndSubgroups("com.belerweb")
        }
    }
    // YACL
    maven {
        name = "Xander Maven"
        url = uri("https://maven.isxander.dev/releases")
        content {
            includeGroupAndSubgroups("dev.isxander")
            includeGroupAndSubgroups("org.quiltmc")
        }
    }
    // YACL快照
    maven {
        name = "Xander Snapshot Maven"
        url = uri("https://maven.isxander.dev/snapshots")
        content {
            includeGroup("dev.isxander")
            includeGroupAndSubgroups("org.quiltmc")
        }
    }
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"
        content {
            includeGroupAndSubgroups("com.github")
        }
    }
}

configurations {
    register("productionRuntimeClient") { configuration ->
        configurations.findAll {
            it.name in [
                    "minecraftLibraries",
                    "loaderLibraries",
                    "minecraftRuntimeLibraries"
            ]
        }.forEach { Configuration superConfigurations ->
            configuration.extendsFrom(superConfigurations)
        }
    }
}

def downloadExternalMod = { String urlStr, String customFilename = null ->
    def client = HttpClient.newBuilder()
            .followRedirects(HttpClient.Redirect.NORMAL)
            .build()
    def request = HttpRequest.newBuilder()
            .uri(URI.create(urlStr))
            .GET()
            .build()
    def response = client.send(request, HttpResponse.BodyHandlers.ofInputStream())
    if (response.statusCode() != 200) {
        throw new IOException("Failed to download: HTTP ${response.statusCode}")
    }
    // 确定文件名
    String filename
    // 1. 优先使用自定义文件名
    if (customFilename != null && !customFilename.isEmpty()) {
        filename = customFilename
    }
    // 2. 尝试从 Content-Disposition 头获取文件名
    else {
        def contentDisposition = response.headers().firstValue("Content-Disposition").orElse(null)
        if (contentDisposition != null && contentDisposition.contains("filename=")) {
            // 提取文件名，处理带引号和不带引号的情况
            def matcher = (contentDisposition =~ /filename=["']?([^"']+)["']?/)
            if (matcher.find()) {
                filename = matcher.group(1)
            }
        }
        // 3. 如果仍未获取到文件名，从URL路径中提取
        if (filename == null) {
            def path = URI.create(urlStr).getPath()
            filename = path.substring(path.lastIndexOf('/') + 1)

            // 如果URL中的文件名是空的或无效，使用默认文件名
            if (filename.isEmpty() || filename.contains("?")) {
                def domain = URI.create(urlStr).getHost()
                def timestamp = System.currentTimeMillis()
                filename = "downloaded-from-${domain}-${timestamp}.jar"
            }
        }
    }
    // 确保文件名以.jar结尾（如果不是）
    if (!filename.endsWith('.jar')) {
        filename += '.jar'
    }
    def libsDir = new File("$rootDir/libs")
    libsDir.mkdirs()
    File target = new File(libsDir, filename)
    if (!target.exists()) {
        println "Downloading external mod: $filename from $urlStr"
        target.withOutputStream { out ->
            response.body().transferTo(out)
        }
        println "Downloaded: ${target.name} (${target.length()} bytes)"
    } else {
        println "File already exists: $filename"
    }
    return target
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    //mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    mappings(loom.officialMojangMappings())
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    implementation include("com.belerweb:pinyin4j:${project.pinyin_version}")           // 拼音
    modImplementation "com.terraformersmc:modmenu:${project.property("modmenu")}"       // 模组菜单

    // masa相关引用
    if (mcVersion > 12006) {
        modImplementation "com.github.sakura-ryoko:malilib:${project.property("malilib")}"
        modImplementation "com.github.sakura-ryoko:litematica:${project.property("litematica")}"
        modImplementation "com.github.sakura-ryoko:tweakeroo:${project.property("tweakeroo")}"
    } else {
        // 冲突根源：tweakeroo 和 litematica 这两个mod都对同一个类 ServerPlayNetworkHandler 中的同一个方法使用了 @Redirect 注入。
        // 优先级问题：litematica 的Mixin优先级是1010，tweakeroo 的优先级是1005。由于litematica优先级更高，它的注入先被应用，导致tweakeroo的注入找不到目标方法而失败。
        // 这应该就是点进设置崩溃的原因吧
        modImplementation "fi.dy.masa.malilib:${project.property("malilib")}"
        modImplementation "curse.maven:litematica-308892:${project.property("litematica")}"
        modImplementation "curse.maven:tweakeroo-297344:${project.property("tweakeroo")}"
    }

    if (mcVersion < 12105) {
        // 箱子追踪
        modImplementation "maven.modrinth:chest-tracker:${project.property("chesttracker")}"
        modImplementation "maven.modrinth:where-is-it:${project.property("whereisit")}"
    }

    if (mcVersion >= 12006) {
        //快捷潜影盒
        def selectedUrl = project.property("quickshulker").toString()
        if (selectedUrl != null) {
            def quickshulkerFile = downloadExternalMod(selectedUrl);
            if (quickshulkerFile.exists()) {
                modImplementation files(quickshulkerFile)
            }
        }
    } else {
        //快捷潜影盒
        modImplementation "net.kyrptonaught:kyrptconfig:${project.property("kyrptconfig")}" // 快捷潜影盒依赖
        modImplementation "curse.maven:quick-shulker-362669:${project.property("quick_shulker")}"
    }

    if (mcVersion >= 12001) {
        modImplementation "dev.isxander:yet-another-config-lib:${project.property("yacl")}"
        modImplementation "red.jackf.jackfredlib:jackfredlib:${project.property("jackfredlib")}"
        modImplementation "com.blamejared.searchables:${project.property("searchables")}"
    } else {
        modImplementation "maven.modrinth:cloth-config:${project.property("cloth_config")}"
        modImplementation "io.github.cottonmc:LibGui:${project.property("LibGui")}"
    }

    if (mcVersion < 11904) {
        modImplementation "me.shedaniel.cloth.api:cloth-api:${project.property("cloth_api")}"
    }

    //这是Fabric平台的运行时包装器，仅运行时需要
    runtimeOnly(project(path: ":fabricWrapper"))
}
def time = new Date().format("yyMMdd", TimeZone.getTimeZone("GMT+08:00"))
version(project.mod_version + "+" + time)
group = project.mod_maven_group

base {
    archivesName.set("$project.mod_archives_base_name-$project.minecraft_version")
}

loom {
    def commonVmArgs = [
            "-Dmixin.debug.export=true",
            "-Dmixin.debug.countInjections=true",
            "-DmixinAuditor.audit=true"
    ]
    runConfigs.named("client") {
        ideConfigGenerated = true
        vmArgs commonVmArgs
        programArgs([
                "--width",
                "1280",
                "--height",
                "720",
                "--username",
                "PrinterTest"
        ])
        runDir("../../run/client")
    }

    runConfigs.named("server") {
        runDir("../../run/server")
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding("UTF-8")
}

def MIXIN_CONFIG_PATHS = ['litematica-printer.mixins.json']
JavaVersion JAVA_COMPATIBILITY
if (mcVersion >= 12005) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_21
} else if (mcVersion >= 11800) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_17
} else if (mcVersion >= 11700) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_16
} else {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_1_8
}
JavaVersion MIXIN_COMPATIBILITY_LEVEL = JAVA_COMPATIBILITY

tasks.withType(JavaCompile).configureEach {
    if (mcVersion >= 12005) {
        it.options.release = 21
    } else if (mcVersion >= 11800) {
        it.options.release = 17
    } else if (mcVersion >= 11700) {
        it.options.release = 16
    } else {
        it.options.release = 8
    }

}

java {

    sourceCompatibility = JAVA_COMPATIBILITY
    targetCompatibility = JAVA_COMPATIBILITY

    withSourcesJar()
}


processResources {
    outputs.upToDateWhen { false }

    from("${rootDir}/LICENSE")

    from("${rootDir}/icon.png") {
        into("assets/${project.mod_id}")
    }

    filesMatching("fabric.mod.json") {
        filter { line ->
            line.trim().startsWith("//") ? null : line
        }

        expand([
                "minecraft_dependency": project.minecraft_dependency,
                "minecraft_version_id": project.minecraft_version.replace(".", "_"),
                "minecraft_version"   : project.minecraft_version,
                "mod_description"     : project.mod_description,
                "mod_homepage"        : project.mod_homepage,
                "mod_id"              : project.mod_id,
                "mod_license"         : project.mod_license,
                "mod_name"            : project.mod_name,
                "mod_sources"         : project.mod_sources,
                "mod_version"         : project.mod_version
        ])
    }

    MIXIN_CONFIG_PATHS.each { mixinConfigPath ->
        filesMatching(mixinConfigPath) {
            filter { s -> s.replace('{{COMPATIBILITY_LEVEL}}', "JAVA_${MIXIN_COMPATIBILITY_LEVEL.ordinal() + 1}") }
        }
    }
}

// configure the maven publication
publishing {
    publications {
        register("mavenJava", MavenPublication) {
            artifactId("${project.mod_id}")
            version("${project.minecraft_version}")
            from(components.java)
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        mavenLocal()

        maven {
            url("$rootDir/publish")
        }
    }
}


ImmutableMap<Object, Object> replaceTokenMap = ImmutableMap.builder()
        .put("@MOD_IDENTIFIER@", project.mod_id)
        .put("@MOD_NAME@", project.mod_name)
        .put("@MINECRAFT_VERSION_IDENTIFY@", project.minecraft_version.replace(".", "_"))
        .build()
ImmutableList<Object> replaceTokenFile = ImmutableList.builder()
        .add("litematica-printerReference")
        .build()

tasks.classes {
    doLast {
        File dir = file("build/classes/java")

        dir.eachFileRecurse {
            String path = it.path.replace(dir.path, "")

            if (path.endsWith(".class") && replaceTokenFile.stream().anyMatch { path.contains(it as String) }) {
                ClassReader cr = new ClassReader(it.newInputStream().bytes)
                ClassNode cn = new ClassNode()
                cr.accept(cn, ClassReader.SKIP_FRAMES | ClassReader.SKIP_DEBUG)

                // ReplaceToken in fields
                cn.fields.each {
                    if (it.desc == "Ljava/lang/String;" && it.value instanceof String) {
                        String value = it.value as String

                        replaceTokenMap.each {
                            value = value.replace(it.key as String, it.value as String)
                        }

                        it.value = value
                    }
                }

                // ReplaceToken in methods
                cn.methods.each {
                    it.instructions.each {
                        if (it instanceof LdcInsnNode) {
                            LdcInsnNode ldc = it as LdcInsnNode

                            if (ldc.cst instanceof String) {
                                String value = ldc.cst as String

                                replaceTokenMap.each {
                                    value = value.replace(it.key as String, it.value as String)
                                }

                                ldc.cst = value
                            }
                        }
                    }
                }

                ClassWriter cw = new ClassWriter(ClassWriter.COMPUTE_FRAMES)
                cn.accept(cw)
                new FileOutputStream(it).write(cw.toByteArray())
            }
        }
    }
}