import groovy.json.JsonBuilder
import groovy.json.JsonSlurper
import java.text.SimpleDateFormat
import java.util.*

plugins {
    id("java-library")
    id("maven-publish")
    id("mod-plugin")
}

// ç”Ÿæˆæ—¶é—´æˆ³
val time = SimpleDateFormat("yyMMdd")
    .apply { timeZone = TimeZone.getTimeZone("GMT+08:00") }
    .format(Date())
    .toString()

var fullProjectVersion: String by extra
if (System.getenv("IS_THIS_RELEASE") == "true") {
    // å‘å¸ƒreleaseä¸å¸¦æ„å»ºå·
    fullProjectVersion = "$modVersion+$time"
} else if (System.getenv("IS_THIS_RELEASE") == "false") {
    //å­—ç¬¦ä¸²è½¬æ•´æ•°å€¼å†è½¬å­—ç¬¦ä¸² ğŸ¤”
    val buildNumber: String? = System.getenv("GITHUB_RUN_NUMBER")
    val intBuildNumber = buildNumber?.toInt()?.plus(175) //å»¶ç»­æ„å»ºå·
    val finalBuildNumber = intBuildNumber?.toString()
    fullProjectVersion = "$modVersion+$time+build.$finalBuildNumber"
} else {
    // æœ¬åœ°æ„å»ºæ·»åŠ localåç¼€
    fullProjectVersion = "$modVersion+$time+local"
}

group = modMavenGroup
version = fullProjectVersion

base {
    archivesName.set("$modArchivesBaseName-versionpack")
}

// è·å–æ‰€æœ‰å­é¡¹ç›®ï¼ˆæ’é™¤å½“å‰é¡¹ç›®ï¼‰
val fabricSubprojects = rootProject.subprojects.filter { it.name != "fabricWrapper" }

// ç¡®ä¿å…ˆè¯„ä¼°æ‰€æœ‰å­é¡¹ç›®
fabricSubprojects.forEach {
    evaluationDependsOn(":${it.name}")
}

tasks {
    // æ”¶é›†å­æ¨¡å— JAR æ–‡ä»¶ä»»åŠ¡
    register("collectSubModules") {
        description = "æ”¶é›†æ‰€æœ‰å­æ¨¡å—çš„ JAR æ–‡ä»¶"
        outputs.upToDateWhen { false }

        // ä¾èµ–æ‰€æœ‰å­é¡¹ç›®çš„ remapJar ä»»åŠ¡
        dependsOn(fabricSubprojects.map { it.tasks.named("remapJar") })

        doFirst {
            // å¤åˆ¶æ‰€æœ‰é‡æ˜ å°„åçš„ JAR æ–‡ä»¶
            copy {
                from(fabricSubprojects.map { sub ->
                    sub.tasks.named("remapJar").get().outputs.files
                })
                into(layout.buildDirectory.dir("tmp/submods/META-INF/jars"))
            }
        }
    }

    // JAR æ‰“åŒ…ä»»åŠ¡
    named<Jar>("jar") {
        outputs.upToDateWhen { false }

        from(rootProject.file("LICENSE"))
        from(layout.buildDirectory.dir("tmp/submods"))
    }

    // èµ„æºå¤„ç†ä»»åŠ¡
    named<ProcessResources>("processResources") {
        outputs.upToDateWhen { false }

        // æ¸…ç†ç›¸å…³ç›®å½•
        delete(layout.buildDirectory.dir("libs"))
        delete(layout.buildDirectory.dir("resources"))
        delete(layout.buildDirectory.dir("tmp/submods/META-INF/jars"))

        dependsOn("collectSubModules")

        val rootIcon = rootProject.file("src/main/resources/assets/$modId/icon.png")
        val resourcesFile = layout.projectDirectory.file("src/main/resources/assets/$wrapperModId/icon.png").asFile
        val buildIconFile = layout.buildDirectory.file("resources/main/assets/$wrapperModId/icon.png").get().asFile

        doLast {
            if (rootIcon.exists()) {
                if (!resourcesFile.exists()) {
                    println("âš  å­é¡¹ç›®æœªæ‰¾åˆ°å›¾æ ‡æ–‡ä»¶ï¼Œå‡†å¤‡ä»æ ¹é¡¹ç›®ä¸­å¤åˆ¶å›¾æ ‡")
                    buildIconFile.parentFile.mkdirs()
                    rootIcon.copyTo(buildIconFile, overwrite = true)
                    println("âœ“ å›¾æ ‡å¤åˆ¶æˆåŠŸ: ${rootIcon.name} -> ${buildIconFile.name}")
                }
            } else {
                println("âš  æ ¹é¡¹ç›®ä¸­æœªæ‰¾åˆ°å›¾æ ‡æ–‡ä»¶ï¼Œè·³è¿‡å›¾æ ‡å¤åˆ¶")
            }
        }

        doLast {
            val jars = ArrayList<Map<String, String>>()
            val jarsDir = layout.buildDirectory.dir("tmp/submods/META-INF/jars").get().asFile

            if (jarsDir.exists() && jarsDir.isDirectory) {
                val jarFiles = jarsDir.listFiles { file ->
                    file.isFile && file.name.endsWith(".jar") &&
                            !file.name.contains("-dev.jar") &&
                            !file.name.contains("-sources.jar") &&
                            !file.name.contains("-shadow.jar")
                }

                jarFiles?.forEach { jarFile ->
                    jars.add(mapOf("file" to "META-INF/jars/${jarFile.name}"))
                }
            }

            val minecraftVersions = mutableListOf<String>()
            fabricSubprojects.forEach { subproject ->
                try {
                    val minecraftVersion = subproject.property("minecraft_dependency") as String
                    if (minecraftVersion.isNotBlank()) {
                        minecraftVersions.add(minecraftVersion)
                        println("æ”¶é›†åˆ° Minecraft ç‰ˆæœ¬: $minecraftVersion")
                    }
                } catch (e: Exception) {
                    println("âš  æ— æ³•ä»å­é¡¹ç›® ${subproject.name} è·å– Minecraft ç‰ˆæœ¬")
                }
            }

            // æ›´æ–° fabric.mod.json æ–‡ä»¶
            val jsonFile = layout.buildDirectory.file("resources/main/fabric.mod.json").get().asFile
            if (jsonFile.exists()) {
                val slurper = JsonSlurper()

                @Suppress("UNCHECKED_CAST")
                val jsonContent = slurper.parse(jsonFile) as MutableMap<String, Any>

                // è®¾ç½® jars æ•°ç»„
                jsonContent["jars"] = jars

                // æ›´æ–° Minecraft ä¾èµ–
                @Suppress("UNCHECKED_CAST")
                val depends = jsonContent["depends"] as? MutableMap<String, Any>
                depends?.put("minecraft", minecraftVersions)

                // å†™å›æ–‡ä»¶
                val builder = JsonBuilder(jsonContent)
                jsonFile.bufferedWriter().use { writer ->
                    writer.write(builder.toPrettyString())
                }

                println("- JAR æ–‡ä»¶æ•°é‡: ${jars.size}")
                jars.forEach { jar ->
                    println("  - ${jar["file"]}")
                }
                println("âœ… Minecraft ä¾èµ–å·²æ›´æ–°ä¸º: $minecraftVersions")
            } else {
                println("è­¦å‘Š: æ‰¾ä¸åˆ°ç”Ÿæˆçš„ fabric.mod.json æ–‡ä»¶: ${jsonFile.absolutePath}")
            }
        }
    }
}